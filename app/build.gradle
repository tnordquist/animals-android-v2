plugins {
    id 'com.android.application'
}

static def getProperty(String propertyFile, String propertyName) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(propertyFile))
    return properties.getProperty(propertyName)
}

static def quoted(String value) {
    return "\"$value\""
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    //noinspection GroovyAssignabilityCheck
    defaultConfig {
        applicationId "edu.cnm.deepdive.animalsandroidv2"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'BASE_URL',
                quoted(getProperty("$projectDir/../local.properties", 'base_url'))
        buildConfigField "String", "CONTENT_FORMAT", quoted(
                getProperty("$projectDir/../local.properties", 'content_format'))
    }

    //noinspection GroovyAssignabilityCheck
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //noinspection GroovyAssignabilityCheck
    buildFeatures {
        //noinspection GroovyAssignabilityCheck
        viewBinding true
    }
}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.media:media:1.4.3'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.fragment:fragment:1.4.0'

    // Basic Android UI components
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Lifecycle components
    def lifecycleVersion = '2.4.0'
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycleVersion"
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // Material Design components
    implementation 'com.google.android.material:material:1.4.0'

    // Retrofit dependencies
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'

    // ReactiveX libraries
    implementation 'io.reactivex.rxjava3:rxjava:3.1.3'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    // Gson (JSON parser) library
    implementation 'com.google.code.gson:gson:2.8.9'

    // Picasso library
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Testing libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}